#Prog Rmt_ReqGroup_ExportCompletion( iPLMIDSet : #In PLMIDSet, oPLMIDSet : #Out PLMIDSet ) : VoidType
{
let RsReqGroupsStruct         (PLMRouteSet)
let RsReqGroupsContent        (PLMRouteSet)
let IdsReqGroupsIn             (PLMIDSet)
let IdsReqGroupsStruct         (PLMIDSet)
let IdsReqGroupsAll            (PLMIDSet)
let IdsContent                 (PLMIDSet)
let IdsReqSpecs                (PLMIDSet)
let IdsReqSpecsAndScope        (PLMIDSet)
let IdsNLVCompletion           (PLMIDSet)

/* Get the reqgroups structure, meaning the whole hierarchy of reqgroups, sub-reqgroups and relations between them */
IdsReqGroupsIn = Restrict( iPLMIDSet , "Class/Requirement Group" )
RsReqGroupsStruct = ExecutePLMFunction( "Requirement", "rmt_nav_reqgroup_structure", IdsReqGroupsIn )
IdsReqGroupsStruct = Ids(RsReqGroupsStruct)


/* Isolate all the reqgroups of the structure, and get their pointed reqspecs, plus the relations between them and their reqgroup */
IdsReqGroupsAll = Restrict( IdsReqGroupsStruct , "Class/Requirement Group" )
RsReqGroupsContent = ExecutePLMFunction( "Requirement", "rmt_nav_reqgroup_children", IdsReqGroupsAll )
IdsContent = Ids(RsReqGroupsContent)


/* Complete procedure to export pointed reqspecs */
IdsReqSpecs = Restrict( IdsContent , "Class/Requirement Specification" )
IdsReqSpecsAndScope = ExecutePLMProcedure( "Rmt_ReqSpec_ExportCompletion", IdsReqSpecs )

/* ------------------------------------------------------------- */
/* Items completion for version graph consistency with NLV stack */
/* IdsReqGroupsIn is added to be sure roots are took into account with NLVCompletion */
IdsNLVCompletion = ExecuteJavaProcedure("com.dassault_systemes.enovia.versioning.core.completion.NLVCompletion", IdsReqGroupsIn + IdsReqGroupsAll)
/* ------------------------------------------------------------- */

/* Provide back all that stuff */
oPLMIDSet = iPLMIDSet
+ IdsReqGroupsStruct
+ IdsContent
+ IdsReqSpecsAndScope
+ IdsNLVCompletion
}
