#Prog DefaultSimulationExport (iPLMIDSet :  #In PLMIDSet,oPLMIDSet :  #Out PLMIDSet) : VoidType
{
let IdsCategoriesAndSo(PLMIDSet)
let IdsProductStructure(PLMIDSet)
let IdsExtDoc(PLMIDSet)
let IdsSimuRep(PLMIDSet)
let IdsSimuPointedTable(PLMIDSet)
let IdsExtDocFromSimuRep(PLMIDSet)
let IdsProdAndChildren(PLMIDSet)
let IdsPublishResult(PLMIDSet)
let IdsSetModel (PLMIDSet) 
let IdsRefInput (PLMIDSet)
let IdsPortParam (PLMIDSet)
let IdsSimuPointedAndObj (PLMIDSet)
let IdsSimuPointed(PLMIDSet)
let IdsScenarioMat(PLMIDSet)
let IdsScenarioMat2(PLMIDSet)

let RouteSetCategoriesAndSo (PLMRouteSet)
let RouteSetModel (PLMRouteSet)
let RouteSetExtDoc (PLMRouteSet)
let RoutePortParam(PLMRouteSet)
let RouteSimuRep (PLMRouteSet)
let RouteSimuPointedTable (PLMRouteSet)
let RouteExtDocFromSimuRep (PLMRouteSet)
let RouteSetProdAndChildren (PLMRouteSet)
let RoutePublishResult (PLMRouteSet)
let RouteSimuPointedAndObj(PLMRouteSet)
let RouteScenarioMat(PLMRouteSet)
let RouteScenarioMat2(PLMRouteSet)
 

/* getting categories + so */
IdsRefInput = Restrict(iPLMIDSet, "SIMObjSimulation/SIMObjSimulationObject") + Restrict(iPLMIDSet, "SIMObjSimulationCategoryAndProdCnx/SIMObjSimulationCategoryReference")
RouteSetCategoriesAndSo = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_retrieveCategories",  IdsRefInput)
IdsCategoriesAndSo = Ids( RouteSetCategoriesAndSo)

/* getting External documents from SO or\and from Categories */
RouteSetExtDoc = ExecutePLMFunction("PLMDocConnection", "PLMDocConnection_retrieveAllDocuments", IdsCategoriesAndSo)
IdsExtDoc = Ids(RouteSetExtDoc)

/* getting Parameter Ports from SO  */
RoutePortParam = ExecutePLMFunction("PLMParameter", "PAR_nav_paramports", IdsCategoriesAndSo)
IdsPortParam = Ids(RoutePortParam)

/* getting system connection + product */
RouteSetModel = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_addSimulatedModel", Restrict(IdsCategoriesAndSo, "SIMObjSimulationCategoryAndProdCnx/SIMObjSimulationCategoryReference"))
IdsSetModel =   Ids(RouteSetModel)

/*getting Product and Children from Product*/
RouteSetProdAndChildren = ExecutePLMFunction("PRODUCTCFG", "ProductCfg_AddChildrenProduct",   Restrict(Ids(RouteSetModel, "last"), "PRODUCTCFG/VPMReference"))
IdsProdAndChildren = Ids(RouteSetProdAndChildren)

/* calling Product Procedure*/
IdsProductStructure  =  ExecutePLMProcedure( "VPMEditor_GetAllRepresentations",  IdsProdAndChildren ) 

/* getting simu rep under category Scenario*/
RouteSimuRep = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_retrieveSimuRep", Restrict(IdsCategoriesAndSo, "SIMObjSimulationCategoryAndProdCnx/SIMObjSimulationCategoryReference"))
IdsSimuRep = Ids(RouteSimuRep)

/* getting material reference under category Scenario*/
RouteScenarioMat = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_retrieveMaterialfromSimuRep", Restrict(IdsSimuRep,"SIMObjSimulationV5Generic/SIMObjSimulationV5RepReferenceGeneric"))
IdsScenarioMat = Ids(RouteScenarioMat) 

/* Get the MaterialDomains of the above Material Reference. */
RouteScenarioMat2 = ExecutePLMFunction("CATMaterialRef", "mat_retrieveDomains", Restrict(IdsScenarioMat,"CATMaterialRef/CATMatReference"))
IdsScenarioMat2 = Ids(RouteScenarioMat2) 

/*getting the Simulation Object pointed through Scenario Rep*/
RouteSimuPointedAndObj =  ExecutePLMFunction("SIMObjSimulationGeneric", "sim_retrieveSOfromScenarioRep",  Restrict(IdsSimuRep,  "SIMObjSimulationV5Generic/SIMObjSimulationV5RepReferenceGeneric"))
IdsSimuPointedAndObj = Ids(RouteSimuPointedAndObj)

/* calling Simu Export Procedure on pointed Simulation through Scenario Rep*/
IdsSimuPointed  =  ExecutePLMProcedure(  "SimulationExportSimple",  Restrict(IdsSimuPointedAndObj,"SIMObjSimulationGeneric/SIMObjSimulationObjectGeneric" )) 

/*Completes with PointedDesignTable for Scenario Rep */
RouteSimuPointedTable = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_AddPointedDesignTable",  Restrict(IdsSimuRep,  "SIMObjSimulationV5Generic/SIMObjSimulationV5RepReferenceGeneric"))
IdsSimuPointedTable = Ids(RouteSimuPointedTable)

/* getting external doc from simu rep */
RouteExtDocFromSimuRep = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_retrieveExternalDocumentfromSimuRep", Restrict(IdsSimuRep, "SIMObjSimulationV5Generic/SIMObjSimulationV5RepReferenceGeneric"))
IdsExtDocFromSimuRep = Ids(RouteExtDocFromSimuRep)

/* getting publish result connection + target of the connection */
RoutePublishResult  = ExecutePLMFunction("SIMObjSimulationGeneric", "sim_addPublishResultCnx", Restrict(IdsCategoriesAndSo, "SIMObjSimulationCategoryAndProdCnx/SIMObjSimulationCategoryReference"))
IdsPublishResult = Ids(RoutePublishResult)
 
 
oPLMIDSet = IdsCategoriesAndSo + IdsExtDoc + IdsPortParam + IdsSetModel + IdsProdAndChildren + IdsProductStructure + IdsSimuRep + IdsScenarioMat + IdsScenarioMat2 + IdsSimuPointedTable + IdsExtDocFromSimuRep + IdsPublishResult + IdsSimuPointed
}
